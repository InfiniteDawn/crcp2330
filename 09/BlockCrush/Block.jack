// BlockCrush (C) Jakob Wells
// Block.jack
// Character object. Controls are supplied by game object, position is tracked by Block.

class Block {
	
	field int posY;


	/* Constructs a new Block object given a Y position */
	constructor Block new(int newY) {
		let posY = newY;
		do drawUpdate();

		return this;
	}

	/* Deallocate memory associated with this object */
	method void dispose () {
		do Memory.deAlloc(this);
		return;
	}

	/* Move Block up. Prevents movement off-screen */
	method void moveUp () {
		if (posY > 0) {
			let posY = posY - 1;
			do drawUpdate();
		}
		return;
	}
	
	/* Move Block down. Prevents movement off-screen */
	method void moveDown () {
		if (posY < 7) {
			let posY = posY + 1;
			do drawUpdate();
		}
		return;
	}

	/* Return the position of the Block for "collision detection" purposes */
	method int getY () {
		return posY;
	}

	/* Draws the Block to the screen as a black box */
	method void drawUpdate () {
		// Clear the old box draw.
		do Screen.setColor(false);
		do Screen.drawRectangle(32,0,63,255);

		// Draw the current box position.
		do Screen.setColor(true);
		do Screen.drawRectangle(32,posY*32,63,(posY + 1)*32 - 1);
		do Screen.setColor(false);
		do Screen.drawRectangle(32 + 4,posY*32 + 4,63 - 4,(posY + 1)*32 - 5);

		return;
	}
}