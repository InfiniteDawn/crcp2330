// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here: >>> Gonna make it in pieces
   
   // Determine if we zero out the result
   Mux16(a=x,b=false,sel=zx,out=myX2);
   Mux16(a=y,b=false,sel=zy,out=myY2);
   
   // Negate the output of that
   Not16(in=myX2,out=Nx);
   Not16(in=myY2,out=Ny);

   // Determine if we use the negated output
   Mux16(a=myX2,b=Nx,sel=nx,out=myX3);
   Mux16(a=myY2,b=Ny,sel=ny,out=myY3);

   // Get the output for both AND and ADD
   Add16(a=myX3,b=myY3,out=myAdd);
   And16(a=myX3,b=myY3,out=myAnd);

   // Determine which function we actually use
   Mux16(a=myAnd,b=myAdd,sel=f,out=funcOut);

   // Negate that output
   Not16(in=funcOut,out=NfuncOut);

   // Determine whether we negate or not
   Mux16(a=funcOut,b=NfuncOut,sel=no,out[0..7]=outX1,out[8..15]=outX2,out[15]=ng);

   // Determine if the output is zero
   Or8Way(in=outX1,out=z1);
   Or8Way(in=outX2,out=z2);
   Or(a=z1,b=z2,out=Nzr);
   Not(in=Nzr,out=zr);

   // Output the Output (Fix for a HACK bug)
   Or16(a[0..7]=outX1,a[8..15]=outX2,b=false,out=out);

}